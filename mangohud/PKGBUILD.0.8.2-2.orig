# Maintainer: Campbell Jones <arch at serebit dot com>
# Co-Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Ranieri Althoff <ranisalt+aur at gmail dot com>
# Contributor: Mark Wagie <mark dot wagie at proton dot me>
_pkgname=MangoHud
pkgname=mangohud
pkgver=0.8.2
pkgrel=2
pkgdesc="A Vulkan overlay layer for monitoring FPS, temperatures, CPU/GPU load and more."
arch=('x86_64')
url="https://github.com/flightlessmango/MangoHud"
license=('MIT')
makedepends=('appstream' 'cmocka' 'git' 'glslang' 'libxnvctrl' 'libxrandr' 'meson' 'python-mako' 'vulkan-headers')
depends=('dbus' 'fmt' 'gcc-libs' 'glfw' 'hicolor-icon-theme' 'libglvnd' 'libx11' 'libxkbcommon' 'python' 'python-matplotlib' 'python-numpy' 'vulkan-icd-loader' 'wayland')
optdepends=('gamescope: Use MangoApp as an overlay within gamescope'
            'libxnvctrl: NVIDIA GPU stats by XNVCtrl')
replaces=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
conflicts=("$pkgname-common" "$pkgname-common-wayland" "$pkgname-common-x11" "mangoapp" "$pkgname-x11" "$pkgname-wayland")
source=("$pkgname-$pkgver.tar.xz::https://github.com/flightlessmango/MangoHud/releases/download/v$pkgver/$_pkgname-v$pkgver-Source.tar.xz")
b2sums=('c6db1fa344638da38969d5bb2fdd1d9c2aa8fced88229efda2e2a3f39a504810fea9ecddefee8364b44666485ab10e05d3f7cf3f5a6450fade28a270653d7918')

prepare() {
    cd "$_pkgname-v$pkgver"

    # Use system cmocka instead of subproject
    sed -i "s/  cmocka = subproject('cmocka')//g" meson.build
    sed -i "s/cmocka_dep = cmocka.get_variable('cmocka_dep')/cmocka_dep = dependency('cmocka')/g" meson.build
}

build() {
    arch-meson "$_pkgname-v$pkgver" build \
        -Dmangoapp=true \
        -Dmangohudctl=true

    meson compile -C build
}

package() {
    meson install -C build --destdir "$pkgdir"

    install -Dm 0644 "$srcdir/$_pkgname-v$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}

check() {
    meson test -C build --print-errorlogs || :
}
